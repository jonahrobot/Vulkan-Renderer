#version 450

struct IndexedIndirectCommand 
{
	uint indexCount;
	uint instanceCount;
	uint firstIndex;
	uint vertexOffset;
	uint firstInstance;
};

layout(binding = 0) uniform UniformBufferObject{
    mat4 view;
    mat4 proj;
	vec4 frustumPlanes[6];
} ubo;

layout(std140, binding = 4) readonly buffer InstanceData {
    vec4 center_point[ ];
};

layout (binding = 3, std430) writeonly buffer IndirectDraws
{
	IndexedIndirectCommand indirectDraws[ ];
};

layout (local_size_x = 64) in;

// Frustum cull logic partially from Sacha Willems Vulkan ComputeCull example.
bool frustum_check(vec4 pos, float radius){

	for (int i = 0; i < 6; i++) 
	{
		if (dot(pos, ubo.frustumPlanes[i]) + radius < 0.0)
		{
			return false;
		}
	}
	return true;
}

void main(){

	uint index = gl_GlobalInvocationID.x; 

	vec4 pos = center_point[index];

	if(frustum_check(pos, 1.0)){
		indirectDraws[index].instanceCount = 1;
	}else{
		indirectDraws[index].instanceCount = 0;
	}
}